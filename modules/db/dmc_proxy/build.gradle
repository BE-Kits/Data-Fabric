import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
        mysqlConnectorVersion = '5.1.45'
    }
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
        mavenCentral()
    }
    dependencies {
        classpath("mysql:mysql-connector-java:${mysqlConnectorVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'net.ltgt.apt' version '0.10'
    id 'org.flywaydb.flyway' version '5.1.3'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.zhuxun.ms'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

defaultTasks 'bootRun'

repositories {
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
    jcenter()
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

ext {
    getOrDefault = { prop, defaultValue ->
        project.hasProperty(prop) ? project.getProperty(prop) : defaultValue
    }

    devDbURL = getOrDefault('devDbURL', 'jdbc:mysql://127.0.0.1:3306/dmc_api')
    devDbUser = getOrDefault('devDbUser', 'root')
    devDbDriverClass = getOrDefault('devDbDriverClass', 'com.mysql.jdbc.Driver')
    devDbPassword = getOrDefault('devDbPassword', '')

    springBootVersion = '2.0.1.RELEASE'
    springCloudVersion = '1.4.4.RELEASE'
    springfoxSwaggerVersion = '2.8.0'
    jjwtVersion = '0.9.0'
    mysqlConnectorVersion = '6.0.6'
    lombokVersion = '1.16.22'

    mybatisSpringBootVersion = '1.3.2'
    mybatisGeneratorCoreVersion = '1.3.6'

    hazalcastVersion = '3.10.2'
}

configurations {
    // ant task
    mbgenerator
}

sourceSets {
    generated {
        java.srcDirs = ["${buildDir}/generated/src/java"]
        resources.srcDirs = ["${buildDir}/generated/src/resources"]
        compileClasspath += sourceSets.main.runtimeClasspath
    }

    main {
        java {
            srcDirs "${buildDir}/generated/src/java"
        }
        resources {
            srcDirs "${buildDir}/generated/src/resources"
        }
    }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-data-jpa"

    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.cloud:spring-cloud-starter-zuul:${springCloudVersion}"
    compile "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatisSpringBootVersion}"
    compile "io.springfox:springfox-swagger2:${springfoxSwaggerVersion}"
    compile "io.springfox:springfox-swagger-ui:${springfoxSwaggerVersion}"
    compile "io.jsonwebtoken:jjwt:${jjwtVersion}"

    compile "com.hazelcast:hazelcast:${hazalcastVersion}"

    runtime "mysql:mysql-connector-java:${mysqlConnectorVersion}"
    runtime 'com.h2database:h2'

    testCompile 'org.springframework.boot:spring-boot-starter-test'

    compile "org.projectlombok:lombok:${lombokVersion}"
    apt "org.projectlombok:lombok:${lombokVersion}"
    compile group: 'io.swagger.core.v3', name: 'swagger-core', version: '2.0.1'

// https://mvnrepository.com/artifact/com.google.code.gson/gson
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'

    // https://github.com/mybatis/generator/issues/349
    mbgenerator "mysql:mysql-connector-java:5.1.45"
    mbgenerator "org.mybatis.generator:mybatis-generator-core:${mybatisGeneratorCoreVersion}"
    mbgenerator 'tk.mybatis:mapper:3.3.9'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Finchley.M7"
    }
}

flyway {
    url = devDbURL
    user = devDbUser
    password = devDbPassword
    schemas = ['dmc_api']
}

task mbgeneratorClean(type: Delete) {
    delete sourceSets.generated.java.srcDirs[0].path, sourceSets.generated.resources.srcDirs[0].path
}

task mbgenerator(dependsOn: [mbgeneratorClean, flywayMigrate]) {
    ant.properties['target_package'] = 'com.zhuxun.dmc.zuul.repository'
    ant.properties['src_java'] = sourceSets.generated.java.srcDirs[0].path
    ant.properties['src_resources'] = sourceSets.generated.resources.srcDirs[0].path
    ant.properties['jdbc_url'] = devDbURL
    ant.properties['jdbc_user'] = devDbUser
    ant.properties['jdbc_password'] = devDbPassword
    ant.properties['jdbc_driver_class'] = devDbDriverClass

    doLast {
        new File((String) ant.properties['src_java']).mkdirs();
        new File((String) ant.properties['src_resources']).mkdirs();

        ant.taskdef(
                name: 'mbgenerator',
                classname: 'org.mybatis.generator.ant.GeneratorAntTask',
                classpath: configurations.mbgenerator.asPath)
        ant.mbgenerator(
                overwrite: 'true',
                configfile: 'src/main/resources/mybatis/generatorConfig.xml',
                verbose: 'true') {
            propertyset {
                propertyref(name: 'target_package')
                propertyref(name: 'src_java')
                propertyref(name: 'src_resources')
                propertyref(name: 'jdbc_url')
                propertyref(name: 'jdbc_user')
                propertyref(name: 'jdbc_password')
                propertyref(name: 'jdbc_driver_class')
            }
        }
    }
}

compileJava.dependsOn(mbgenerator)

processResources {
    with copySpec {
        from 'src/main/resources'
        include '**/application*.yml'
        include '**/application*.yaml'
        include '**/application*.properties'
        project.properties.findAll().each {
            prop ->
                if (prop.value != null) {
                    filter(ReplaceTokens, tokens: [(prop.key): prop.value])
                    filter(ReplaceTokens, tokens: [('project.' + prop.key): prop.value])
                }
        }
    }
}

processResources {
    with copySpec {
        from 'src/main/resources'
        include '**/application*.yml'
        include '**/application*.yaml'
        include '**/application*.properties'
        project.properties.findAll().each {
            prop ->
                if (prop.value != null) {
                    filter(ReplaceTokens, tokens: [(prop.key): prop.value])
                    filter(ReplaceTokens, tokens: [('project.' + prop.key): prop.value])
                }
        }
    }
}

test {
    systemProperty("devDbURL", devDbURL)
    systemProperty("devDbUser", devDbUser)
    systemProperty("devDbPassword", devDbPassword)
}
